import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}


apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'


group 'com.scienceminer.glutton'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

// The main class of the application
mainClassName = 'com.scienceminer.lookup.web.LookupServiceApplication'
tasks.run.workingDir = rootProject.rootDir


repositories {
    mavenCentral()

}

sourceSets.main.resources {
    srcDirs = ["src/main/resources", "data/config"];
}

processResources {
    filesMatching('**/*.config.yml') {
        filter {
            it.replace('project.version', project.property('version_placeholder'))
        }
    }
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.easymock:easymock:3.5"
    testCompile "com.google.code.gson:gson:2.8.1"

    compile "io.dropwizard:dropwizard-core:1.3.5"
    compile "io.dropwizard:dropwizard-assets:1.3.5"
    compile "com.hubspot.dropwizard:dropwizard-guicier:1.3.5.0"
    compile "io.dropwizard:dropwizard-testing:1.3.5"
    compile "io.dropwizard:dropwizard-forms:1.3.5"
    compile "io.dropwizard:dropwizard-client:1.3.5"
    compile "io.dropwizard:dropwizard-auth:1.3.5"
    compile "io.dropwizard.metrics:metrics-core:4.0.0"
    compile "io.dropwizard.metrics:metrics-servlets:4.0.0"

    compile "com.rockymadden.stringmetric:stringmetric-core_2.10:0.27.3"

    compile 'org.lmdbjava:lmdbjava:0.6.1'
    compile 'de.ruedigermoeller:fst:2.56'
    compile 'org.xerial.snappy:snappy-java:1.1.7.2'

    compile 'org.elasticsearch.client:elasticsearch-rest-high-level-client:6.5.1'

    compile 'org.apache.commons:commons-collections4:4.1'

    compile group: 'org.tukaani', name: 'xz', version: '1.8'
}

shadowJar {
    classifier = 'onejar'
    mergeServiceFiles()
    zip64 true
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

distZip {
    classifier = 'zip'
}

distTar {
    classifier = 'tar'
}

artifacts {
    archives shadowJar
}