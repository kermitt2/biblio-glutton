buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}


apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'


group 'com.scienceminer.glutton'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

// The main class of the application
mainClassName = 'com.scienceminer.lookup.web.LookupServiceApplication'
tasks.run.workingDir = rootProject.rootDir


repositories {
    mavenCentral()

}

sourceSets.main.resources {
    srcDirs = ["src/main/resources", "data/config"];
}

processResources {
    filesMatching('**/*.config.yml') {
        filter {
            it.replace('project.version', project.property('version_placeholder'))
        }
    }
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.easymock:easymock:3.5"

    compile "com.google.code.gson:gson:2.8.1"

    compile "io.dropwizard:dropwizard-core:1.3.29"
    compile "io.dropwizard:dropwizard-assets:1.3.29"
    compile "com.hubspot.dropwizard:dropwizard-guicier:1.3.5.2"
    compile "io.dropwizard:dropwizard-testing:1.3.29"
    compile "io.dropwizard:dropwizard-forms:1.3.29"
    compile "io.dropwizard:dropwizard-client:1.3.29"
    compile "io.dropwizard:dropwizard-auth:1.3.29"
    compile "io.dropwizard.metrics:metrics-core:4.0.5"
    compile "io.dropwizard.metrics:metrics-servlets:4.0.5"
    compile group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.11.2'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.20'

    compile "com.rockymadden.stringmetric:stringmetric-core_2.10:0.27.3"

    //Parsing XML/Json
    compile group: 'org.codehaus.woodstox', name: 'stax2-api', version: '4.0.0'
    compile group: 'org.codehaus.woodstox', name: 'woodstox-core-asl', version: '4.4.1'

    compile 'org.lmdbjava:lmdbjava:0.8.1'
    compile 'de.ruedigermoeller:fst:2.56'
    compile 'org.xerial.snappy:snappy-java:1.1.7.2'

    compile 'org.elasticsearch.client:elasticsearch-rest-high-level-client:6.8.13'

//    compile group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.3'
    compile 'org.apache.commons:commons-collections4:4.1'
    compile 'commons-io:commons-io:2.6'

    compile group: 'org.tukaani', name: 'xz', version: '1.8'

    compile "com.fasterxml.jackson.core:jackson-core:2.9.10"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.10"
    compile "com.fasterxml.jackson.module:jackson-module-afterburner:2.9.10"
}

distributions {
    shadow {
        contents {
            from(new File(rootProject.rootDir, "data/config")) {
                into "data/config"
            }
        }
    }
}

shadowJar {
    classifier = 'onejar'
    mergeServiceFiles()
    zip64 true
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

distTar.enabled = false
distZip.enabled = false
shadowDistTar.enabled = false

artifacts {
    archives shadowJar
    archives shadowDistZip
//    archives shadowDistTar
}